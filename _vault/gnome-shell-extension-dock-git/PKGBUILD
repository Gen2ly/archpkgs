# Maintainer: Todd Partridge <http://https://github.com/Gen2ly/archpkgs>

pkgname=gnome-shell-extension-dock-git
pkgver=20120613
pkgrel=1
pkgdesc="A dock for the GNOME Shell - display favorite and running applications"
arch=('any')
url="https://extensions.gnome.org/extension/17/dock/"
license=('GPL3')
depends=('gnome-shell' 'gtk3')
makedepends=('git' 'gnome-common' 'intltool')

_gitroot="git://git.gnome.org/gnome-shell-extensions"
_gitname="gnome-shell-extensions"
_gittagd="3.4.0"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot #--depth=1
  fi

  msg "GIT checkout done or server timeout"

  cd "$srcdir/$_gitname"
  # Checkout current Arch Linux "gnome-shell" version
  git checkout "$_gittagd"

  ./autogen.sh --prefix=/usr --enable-extensions="dock"
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR=${pkgdir} install
  # Edit metadata to support current shell version
  #sed -i 's/^"shell-version".*$/"shell-version": [ "3.4.1" ],/' ${pkgdir}/usr/share/gnome-shell/extensions/dock@gnome-shell-extensions.gcampax.github.com/metadata.json && \
  msg "Edited for current Arch GNOME shell version"
  # Remove conflicting locale files from package
  rm -r ${pkgdir}/usr/share/locale  && \
  msg "Removed package locale files"
}
